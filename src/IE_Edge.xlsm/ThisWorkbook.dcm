VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "ThisWorkbook"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Option Explicit
'※ThisWorkbookに記述
'※[Microsoft Internet Controls]要参照
 
Private WithEvents WebView As SHDocVw.WebBrowser
Attribute WebView.VB_VarHelpID = -1
 
Public Sub Sample2()
  Dim frmBrowser As Object
  Const ComponentName = "UserForm1"
  Const ControlName = "WebView"
  Const CtrlWidth = 640
  Const CtrlHeight = 480
   
  '事前にUserForm削除
  On Error Resume Next
  With Application.VBE.ActiveVBProject.VBComponents
    .Remove .Item(ComponentName)
  End With
  On Error GoTo 0
   
  'UserForm(VBIDE.VBComponent)追加
  With Application.VBE.ActiveVBProject.VBComponents.Add(3) 'vbext_ct_MSForm
    .Name = ComponentName
    .Properties("Caption").Value = "WebBrowser"
    .Properties("BackColor").Value = &HFFFFFF
    .Properties("Width").Value = CtrlWidth
    .Properties("Height").Value = CtrlHeight
    .Properties("StartUpPosition").Value = 1
    .Properties("ShowModal").Value = False
     
    'WebBrowser(MSForms.Control)追加
    With .Designer.Controls.Add("Shell.Explorer.2")
      .Name = ControlName
      .Top = 0
      .Left = 0
      .Width = CtrlWidth
      .Height = CtrlHeight
    End With
  End With
   
  Set frmBrowser = UserForms.Add(ComponentName)
  frmBrowser.Show
   
  Set WebView = frmBrowser.Controls(ControlName)
  With WebView
    '気休め程度にヘッダーにUser-Agent追加
    .Navigate2 _
      URL:="https://www.google.com/?hl=ja", _
      Headers:="User-Agent: Mozilla/5.0 (Windows NT 10.0; Win64; x64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/90.0.4430.212 Safari/537.36 Edg/90.0.818.66"
    Do While .Busy = True Or .ReadyState <> 4
      DoEvents
    Loop
    .Document.getElementsByName("q")(0).Value = "初心者備忘録"
    .Document.getElementsByName("btnK")(0).Click
  End With
End Sub
 
Private Sub WebView_DocumentComplete(ByVal pDisp As Object, URL As Variant)
  Debug.Print "DocumentComplete:" & URL
End Sub
